buildscript {
    repositories {
        maven { url 'https://maven.enaium.cn' }
        mavenCentral()
    }

    dependencies {
        classpath 'cn.enaium.humblemc:HumbleGradle:1.0.0'
    }
}

apply plugin: 'cn.enaium.humblemc.gradle'
apply plugin: 'java'
apply plugin: 'maven-publish'

group 'org.dimdev.riftloader'
version '2.0.0-' + releaseTime()

def asmVersion = '9.2'

def releaseTime() {
    return new Date().format("yyyyMMddHHmm", TimeZone.getTimeZone("GMT+08:00"))
}

dependencies {
    implementation 'cn.enaium:launchwrapper:1.3.0'
    implementation 'org.spongepowered:mixin:0.8.5'
    implementation("org.ow2.asm:asm:$asmVersion")
    implementation("org.ow2.asm:asm-analysis:$asmVersion")
    implementation("org.ow2.asm:asm-commons:$asmVersion")
    implementation("org.ow2.asm:asm-tree:$asmVersion")
    implementation("org.ow2.asm:asm-util:$asmVersion")
}

humble {
    minecraft {
        version = '1.18.2'
        tweakClasses = ['org.dimdev.riftloader.launch.RiftLoaderTweaker']
    }
}

targetCompatibility = sourceCompatibility = JavaVersion.VERSION_17

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

publishing {
    repositories {
        def ENV = System.getenv()
        if (ENV.MAVEN_URL) {
            maven {
                url ENV.MAVEN_URL
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version
            from components.java
            artifact sourcesJar
        }
    }
}